package openlegacy;

import org.openlegacy.designtime.terminal.analyzer.modules.login.LoginScreenFact;


rule "identify-login"
ruleflow-group "identify-screen-types"
no-loop
salience 100 
no-loop
when
	$ruleParameterSet			  : RuleParametersSet(ruleId == "identify-login")
	$userFieldRegex  			  : String()  from (String)$ruleParameterSet.getRuleParameters().get("userFieldRegex");
	$errorMessageLinesFromButtom  : String()  from (String)$ruleParameterSet.getRuleParameters().get("errorMessageLinesFromButtom");
	$errorMessageColumn 		  : Integer() from (Integer)$ruleParameterSet.getRuleParameters().get("errorMessageColumn");
	
	// there is a screen definition
	$screenEntityDefinition: ScreenEntityDesigntimeDefinition()

	// and a snapshot from the 1st screen definition
	$snapshot: TerminalSnapshot() from $screenEntityDefinition.getSnapshot()
	
	$userField: TerminalField(editable == true) from $snapshot.getFields()
	$userLabelField: TerminalField(position.row == $userField.position.row, position.column < $userField.position.column, 
															editable == false,value matches $userFieldRegex) from $snapshot.getFields();
	not TerminalField(this != $userField,editable == true, 
						position.row == $userField.position.row, 
						position.column - $userLabelField.position.column < $userField.position.column - $userLabelField.position.column) 
		from $snapshot.getFields()
	// collect all the password fields
	$passwordFields: List(size >= 1) from collect (TerminalField(password == true, position.row >= $userField.position.row) from $snapshot.getFields())

	// label is not empty	
	$passwordField: TerminalField(value not matches "[\\s]+") from $passwordFields.get(0)
	
	$passwordLabelFields: List() from collect (TerminalField(position.row == $passwordField.position.row, position.column < $passwordField.position.column, editable == false) from $snapshot.getFields())

	$errorLine: Integer() from $snapshot.getSize().getRows() - $errorMessageLinesFromButtom  
	
then
	// grab 1st password label field fs exists
	TerminalField passwordLabelField = $passwordLabelFields.size() > 0 ? (TerminalField)$passwordLabelFields.get(0) : null;
	
	screenEntityDefinitionsBuilder.processFact($screenEntityDefinition, 
			new LoginScreenFact($userField,$userLabelField,$passwordField,passwordLabelField,new SimpleTerminalPosition($errorLine,$errorMessageColumn)));
	update($screenEntityDefinition);
	
end
